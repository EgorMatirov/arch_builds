# Maintainer: Aleksey Filippov <sarum9in@gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=qwt
pkgver=6.1.0
pkgrel=2
pkgdesc="Qt Widgets for Technical Applications"
arch=('i686' 'x86_64')
url="http://qwt.sourceforge.net/"
depends=('qt5-base' 'qt5-svg')
license=("custom:$pkgname")
source=("http://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.bz2" \
        "qwtconfig-archlinux.pri")
sha1sums=('48a967038f7aa9a9c87c64bcb2eb07c5df375565'
          'aba0fc75f6d450237028b7b143bdcf05a79cabb3')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  cp ${srcdir}/qwtconfig-archlinux.pri qwtconfig.pri

  # build qwt:
  qmake qwt.pro
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make INSTALL_ROOT=${pkgdir} install

  # install web designer plugin
  install -d ${pkgdir}/usr/lib/qt/plugins/designer/
  install -Dm755 designer/plugins/designer/libqwt_designer_plugin.so \
    ${pkgdir}/usr/lib/qt/plugins/designer/

  # needed by qmake (FS#30407)
  install -d "${pkgdir}"/usr/lib/qt/mkspecs/features/
  install -m644 qwtconfig.pri qwt.prf qwtfunctions.pri \
    "${pkgdir}"/usr/lib/qt/mkspecs/features/

  # move man pages
  mv "${pkgdir}"/usr/share/doc/qwt/man/ "${pkgdir}"/usr/share/

  # install licence
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
