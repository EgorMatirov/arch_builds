# Maintainer: Filippov Aleksey <sarum9in@gmail.com>
pkgname=yandex.contest.invoker-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Yandex.Contest.Invoker"
arch=('i686' 'x86_64')
url="https://github.com/sarum9in/yandex_contest_invoker"
license=('GPL')
groups=()
depends=('boost-libs' 'yandex.contest.common-git' 'yandex.contest.system-git')
makedepends=('git' 'boost')
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=
source=("$pkgname::git://github.com/sarum9in/yandex_contest_invoker.git"
        'lxc.conf'
        'yandex-contest-invoker.sh')
noextract=()
md5sums=('SKIP'
         '5c9256ac9a3e8cfcd849bad73997c8d7'
         '035bc83489dbed92e8bed93b2915c002')

pkgver() {
  cd "$srcdir/$pkgname"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$pkgname"
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make $MAKEFLAGS
}

check() {
  cd "$srcdir/$pkgname/build"
  #make test
}

package() {
  cd "$srcdir/$pkgname/build"
  make DESTDIR="$pkgdir/" install
  install -m 644 -D "$srcdir/lxc.conf" "$pkgdir/etc/yandex/contest/invoker/lxc.conf"
  sed -r "s|__ARCH__|$CARCH|" -i "$pkgdir/etc/yandex/contest/invoker/lxc.conf"
  install -m 755 -D "$srcdir/yandex-contest-invoker.sh" "$pkgdir/etc/profile.d/yandex-contest-invoker.sh"
  install -d "$pkgdir/var/lib/yandex/contest/invoker/lxc"
}
